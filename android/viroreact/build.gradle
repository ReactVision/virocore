apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 33
    namespace "com.viro.viroreact"
    buildFeatures {
        buildConfig = true
    }  
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
        ndkVersion = "25.2.9519653"

        ndk {
            // React Native does not support arm64-v8a yet, so don't bother including
            // it here.
            // Tracking issue for RN: https://github.com/facebook/react-native/issues/2814
            abiFilters "arm64-v8a", "armeabi-v7a"
        }

        // Note that viro_arcore does not use STL, so we can still use static STL in viro_native
        // (thereby avoiding conflicts with React Android's shared STL)
        externalNativeBuild {
            cmake {
                targets "viro_renderer"
                cppFlags "-std=c++14 -frtti -fexceptions"
            }
        }

        // Used to differentiate virocore from viroreact in code
        buildConfigField "String", "VIRO_PLATFORM", "\"VIRO_REACT\""
    }

    sourceSets {
        main {
            // Use the /sharedCode directory for the source, resources, and assets
            java.srcDirs = ['../sharedCode/src/main/java']
            res.srcDirs = ['../sharedCode/src/main/res']
            assets.srcDirs = ['../sharedCode/src/main/assets']

            // Package both the /sharedCode jniLibs viroar's build/natives libs into the AAR
            jniLibs.srcDirs = ['../sharedCode/src/main/jniLibs', '../viroar/build/natives/jni']
            manifest.srcFile '../sharedCode/src/main/AndroidManifest.xml'
        }
    }

    externalNativeBuild {
        cmake {
            path "../sharedCode/CMakeLists.txt"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    packagingOptions {
        // The NDK ABI filters above prevent us from building arm64-v8a and x86 architectures for
        // viro_renderer and viro_arcore, but we also have to stop gradle from copying all of our
        // arm64-v8a and x86 dependencies. We do that through these packaging options.
        exclude '**/x86/**'

        pickFirst 'lib/armeabi-v7a/libBulletDynamics.so'
        pickFirst 'lib/armeabi-v7a/libgvr.so'
        pickFirst 'lib/armeabi-v7a/libgvr_audio.so'
        pickFirst 'lib/armeabi-v7a/libfreetyped.so'
        pickFirst 'lib/armeabi-v7a/libBulletCollision.so'
        pickFirst 'lib/armeabi-v7a/libLinearMath.so'
        pickFirst 'lib/armeabi-v7a/libvrapi.so'
        pickFirst 'lib/armeabi-v7a/libBulletSoftBody.so'
        pickFirst 'lib/arm64-v8a/libBulletDynamics.so'
        pickFirst 'lib/arm64-v8a/libgvr.so'
        pickFirst 'lib/arm64-v8a/libgvr_audio.so'
        pickFirst 'lib/arm64-v8a/libfreetyped.so'
        pickFirst 'lib/arm64-v8a/libBulletCollision.so'
        pickFirst 'lib/arm64-v8a/libLinearMath.so'
        pickFirst 'lib/arm64-v8a/libvrapi.so'
        pickFirst 'lib/arm64-v8a/libBulletSoftBody.so'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:1.0.0"
    implementation 'androidx.media3:media3-exoplayer:1.1.1'
    implementation 'androidx.media3:media3-exoplayer-dash:1.1.1'
    implementation 'androidx.media3:media3-exoplayer-hls:1.3.1'
    implementation 'androidx.media3:media3-exoplayer-smoothstreaming:1.1.1'
    implementation project(':libs:gvr')
    implementation 'com.google.ar:core:1.43.0'
    implementation 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-7'
}

task copyReleaseAAR(type: Copy) {
    from('build/outputs/aar')

    // Change the next line to reflect the path to your viro repo
    into('../../../viro/android/viro_renderer/')
    include('viroreact-release.aar')
    rename('viroreact-release.aar', 'viro_renderer-release.aar')
}

// We have to add dependencies to Android tasks in this deferred way because these tasks
// are dynamically generated.
//
// Before assembling release, implementation :viroar:assembleRelease, which will place its .so
// products in its build/natives folder. The jniLibs source set above will ensure these
// .so files are included in our final AAR.
tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.dependsOn ':viroar:assembleRelease'
        task.finalizedBy 'copyReleaseAAR'
    }
}

task copyDebugAAR(type: Copy) {
    from('build/outputs/aar')

    // Change the next line to reflect the path to your viro repo
    into('../../../viro/android/viro_renderer/')
    include('viroreact-debug.aar')
    rename('viroreact-debug.aar', 'viro_renderer-release.aar')
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleDebug') {
        task.dependsOn ':viroar:assembleDebug'
        task.finalizedBy 'copyDebugAAR'
    }
}

// project.afterEvaluate {
//     publishing {
//         publications {
//             release(MavenPublication) {
//                 from components.release
//             }
//         }
//     }
// }
